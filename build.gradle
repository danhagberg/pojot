buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'net.researchgate:gradle-release:2.3.4'
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'net.researchgate.release'

group 'net.digitaltsunami'
archivesBaseName = 'pojot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile 'org.testng:testng:6.9.10'
}

test {
    useTestNG()
}

// -------- Publishing to OSS SonaType -----------

task createJar(type: Jar)

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar, createJar
}

signing {
    sign configurations.archives
}

uploadArchives {

  javadoc.failOnError = false

  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Pojot - POJO Tester'
        packaging 'jar'
        artifactId 'pojot'
        description 'A library to drive testing of POJOs by validating simple getter and setters, equals, hashcode, and to string methods.'
        url 'https://github.com/danhagberg/pojot'

        scm {
          connection 'scm:git:git@github.com:danhagberg/pojot.git'
          developerConnection 'scm:git:git@github.com:danhagberg/pojot.git'
          url 'https://github.com/danhagberg/pojot'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
      }
    }
  }
}
// -------- End of Publishing to OSS SonaType -----------
